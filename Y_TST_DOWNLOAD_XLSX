*&---------------------------------------------------------------------*
*& Report  Y_TST_DOWNLOAD_XLSX
*&---------------------------------------------------------------------*
REPORT y_tst_download_xlsx.

DATA:
gt_outtab TYPE TABLE OF pa0001.

DATA:
gr_table TYPE REF TO cl_salv_table,
lv_xml   TYPE xstring,
lv_dest  TYPE string.

START-OF-SELECTION.

  SELECT * INTO TABLE gt_outtab
  UP TO 2 ROWS
  FROM pa0001 CLIENT SPECIFIED
  WHERE mandt = sy-mandt.

* SALV Object
  TRY .

      cl_salv_table=>factory(
        IMPORTING
          r_salv_table = gr_table
        CHANGING
          t_table      = gt_outtab ).

    CATCH cx_salv_msg.

  ENDTRY.

* Convert internal table to xml (format xlsx)
  lv_xml = gr_table->to_xml( xml_type = if_salv_bs_xml=>c_type_xlsx ).


  CASE cl_gui_alv_grid=>offline( ).
    WHEN 0. "Download XLSX file Online :)

* Convert xml xstring to internal table so_raw255

      DATA:
      lt_xml  TYPE TABLE OF so_raw255,
      lv_size TYPE i.

      CALL FUNCTION 'SCMS_XSTRING_TO_BINARY'
        EXPORTING
          buffer        = lv_xml
        IMPORTING
          output_length = lv_size
        TABLES
          binary_tab    = lt_xml.

      lv_dest = 'C:\TEMP\DUMMY.XLSX'.
      CALL METHOD cl_gui_frontend_services=>gui_download
        EXPORTING
          bin_filesize            = lv_size
          filename                = lv_dest
          filetype                = 'BIN'
        CHANGING
          data_tab                = lt_xml
        EXCEPTIONS
          file_write_error        = 1
          no_batch                = 2
          gui_refuse_filetransfer = 3
          invalid_type            = 4
          no_authority            = 5
          unknown_error           = 6
          header_not_allowed      = 7
          separator_not_allowed   = 8
          filesize_not_allowed    = 9
          header_too_long         = 10
          dp_error_create         = 11
          dp_error_send           = 12
          dp_error_write          = 13
          unknown_dp_error        = 14
          access_denied           = 15
          dp_out_of_memory        = 16
          disk_full               = 17
          dp_timeout              = 18
          file_not_found          = 19
          dataprovider_exception  = 20
          control_flush_error     = 21
          not_supported_by_gui    = 22
          error_no_gui            = 23
          OTHERS                  = 24.

      IF sy-subrc <> 0.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                   WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      ENDIF.

    WHEN OTHERS. " Download XLSX file Background :)

      FIELD-SYMBOLS:
      <fs_xml> TYPE so_raw255.

      lv_dest = '\\SAP-SHARE-DEV\sap-dev-ftp\DEV\OUT\HR\DUMMY.XLSX'.

      OPEN DATASET lv_dest IN BINARY MODE FOR OUTPUT.
      IF sy-subrc = 0.
        TRANSFER lv_xml TO lv_dest.
      ENDIF.
      CLOSE DATASET lv_dest.

  ENDCASE.
